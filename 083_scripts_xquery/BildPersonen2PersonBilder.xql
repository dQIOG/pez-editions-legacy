(: gruppiert BildPersonen.xml (d.i. der Export der Bild-Personen relations-Tabelle aus UNIDAM) nach Personen um :)
<PersonBilder>
{comment {"generated by BildPersonen2PersonenBilder.xql "||current-dateTime()}}
{
    for $pid in distinct-values(//Person/@id)
    let $p := (//Person[@id = $pid])[1]
    let $bilder := //BildPerson[Person/@id = $p/@id]
    let $npid := if ($pid castable as xs:integer) then xs:integer($pid) else 0
    order by $npid ascending
    return 
    <Person id="{$p/@id}" vorname="{$p/Vorname}" name="{$p/Name}" occurences="{count($bilder)}">{
        for $b in $bilder
        return <Bild bildid="{$b/@bildid}" relation="{$b/@relation}">{$b/BildBeschreibung}</Bild>
    }</Person>
}</PersonBilder>